# List available commands
default:
    @just --list

# Sync git submodules
sync:
    git submodule update --init --recursive

# Build the s2 CLI binary (includes lite subcommand)
build *args: sync
    cargo build --locked --release -p s2-cli {{args}}

# Run clippy linter
clippy *args: sync
    cargo clippy --workspace --all-features --all-targets {{args}} -- -D warnings --allow deprecated

# Ensure cargo-deny is installed
_ensure-deny:
    @cargo deny --version > /dev/null 2>&1 || cargo install cargo-deny

# Run cargo-deny checks
deny *args: _ensure-deny
    cargo deny check {{args}}

# Ensure nightly toolchain is installed
_ensure-nightly:
    @rustup toolchain list | grep -q nightly || (echo "❌ Nightly toolchain required. Run: rustup toolchain install nightly" && exit 1)

# Format code with rustfmt
fmt: _ensure-nightly
    cargo +nightly fmt

# Ensure cargo-nextest is installed
_ensure-nextest:
    @cargo nextest --version > /dev/null 2>&1 || cargo install cargo-nextest

# Run tests with nextest (excludes live integration tests that need a server or credentials)
test *args: sync _ensure-nextest
    cargo nextest run --workspace --all-features -E 'not ((package(s2-cli) & binary(integration)) or (package(s2-sdk) & (binary(account_ops) or binary(basin_ops) or binary(metrics_ops) or binary(stream_ops))))' {{args}}

# Run CLI integration tests (requires s2 lite server running)
test-cli-integration: sync _ensure-nextest
    S2_ACCESS_TOKEN=test S2_ACCOUNT_ENDPOINT=http://localhost S2_BASIN_ENDPOINT=http://localhost \
    cargo nextest run -p s2-cli --test integration

# Run SDK integration tests (requires S2_ACCESS_TOKEN and optional custom endpoints)
test-sdk-integration: sync _ensure-nextest
    cargo nextest run -p s2-sdk --test account_ops --test basin_ops --test metrics_ops --test stream_ops

# Verify Cargo.lock is up-to-date
check-locked:
    cargo metadata --locked --format-version 1 >/dev/null

# Install git hooks from hooks/
install-hooks:
    ln -sf `pwd`/hooks/pre-commit .git/hooks/pre-commit
    @echo "Git hooks installed"

# Clean build artifacts
clean:
    cargo clean

# Run s2-lite
lite *args:
    cargo run --release -p s2-cli -- lite {{args}}
